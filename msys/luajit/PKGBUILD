pkgbase=luajit
pkgname=($pkgbase $pkgbase-devel)
_pkgver=2.1.0-beta3
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=(i686 x86_64)
url=https://luajit.org/
license=(MIT)
source=(
	https://luajit.org/download/LuaJIT-$_pkgver.tar.gz
	msys-compat.patch)
sha512sums=(
	c44e967a0f671ed32b55aee810bc8b3b63737a2d7363b8984ae1949b24f98dbb3f9be7c1e10239fdeb96a3e3c836f606342cbd61838cf9bcadb077443eb5bc12
	66edcd5904047c38ff940aa3b71f7034fd34f960c97c90fa2fe1816cef2c837fec09358839cd734d608a3ee2f19fcdb575ae1fc5f2b7c33547096d1e4e444a31)

prepare() {
	cd LuaJIT-$_pkgver
	patch -Np1 -i "$srcdir"/msys-compat.patch
}

build() {
	cd LuaJIT-$_pkgver/src
	make \
		PREFIX=/usr \
		LUAJIT_SO='msys-luajit-$(ABIVER)-$(MAJVER).dll' \
		TARGET_XSHLDFLAGS='-shared -fPIC -Wl,--out-implib,libluajit-$(ABIVER).dll.a' \
		TARGET_STRIP=:
}

package_luajit() {
	cd LuaJIT-$_pkgver
	install -Dm755 src/luajit.exe "$pkgdir"/usr/bin/luajit-$_pkgver.exe
	ln -s luajit-$_pkgver.exe "$pkgdir"/usr/bin/luajit.exe
	install -Dm644 src/msys-luajit-5.1-${_pkgver%%.*}.dll -t "$pkgdir"/usr/bin
	install -dm755 "$pkgdir"/usr/{lib,share}/luajit/5.1
	install -Dm755 src/jit/*.lua -t "$pkgdir"/usr/share/luajit-$_pkgver/jit
	install -Dm644 etc/luajit.1 -t "$pkgdir"/usr/share/man/man1
}

package_luajit-devel() {
	depends=($pkgbase=$pkgver)

	cd LuaJIT-$_pkgver
	install -Dm644 src/{lua.h,lualib.h,lauxlib.h,luaconf.h,lua.hpp,luajit.h} -t "$pkgdir"/usr/include/luajit-${_pkgver%.*}
	install -Dm644 src/libluajit-5.1.dll.a -t "$pkgdir"/usr/lib
	install -Dm644 etc/luajit.pc -t "$pkgdir"/usr/lib/pkgconfig
}
