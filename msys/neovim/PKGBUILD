pkgname=neovim
pkgver=0.5.0
pkgrel=2
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs'
arch=(i686 x86_64)
url=https://neovim.io
license=(custom:neovim)
depends=(libtermkey libuv msgpack-c unibilium libvterm luajit libluv tree-sitter)
makedepends=(cmake ninja gperf libtermkey-devel libuv-devel msgpack-c-devel unibilium-devel libvterm-devel luajit-devel libluv-devel tree-sitter-devel luajit-mpack luajit-lpeg)
groups=(editors)
source=(
	https://github.com/neovim/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz
	spawn-pipe.patch)
sha512sums=(
	f6649f804faabb4104d3b28283932e40358c23990961f4ca7b380089318da312e59242746cee06387f4d881dd6514abbfec79c4063482383adfb4106e9e7a3a4
	960651aec171ac073988bb1b99c66f2ceb284484d697c13807a32b55ab24a1b9590890136062e69bf9c2f11ad89dea89987a56416b35356549fe9bae65623bc2)

prepare() {
	cd $pkgname-$pkgver
	patch -Np1 -i "$srcdir"/spawn-pipe.patch
}

build() {
	mkdir -p build
	cd build
	cmake ../$pkgname-$pkgver \
		-G Ninja \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_LTO=OFF \
		-DUSE_BUNDLED=OFF
	ninja
}

check() {
	cd build
	./bin/nvim --version
	./bin/nvim --headless -u NONE -i NONE -c ':quit'
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
	install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
