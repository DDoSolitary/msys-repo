pkgbase=msgpack-c
pkgname=($pkgbase $pkgbase-devel)
pkgver=3.3.0
pkgrel=1
pkgdesc='An efficient object serialization library'
arch=(i686 x86_64)
url=https://msgpack.org/
license=(Apache)
makedepends=(cmake)
source=(https://github.com/msgpack/$pkgbase/releases/download/cpp-${pkgver}/msgpack-${pkgver}.tar.gz)
sha512sums=(ad3e32edc8c6afd70282b3d4b493c2ffe74a697c41bd1f39030c5b4752cccefaa965bc049d4c2e63103a210bf714dc3bddd474691bc067d1475ae017593f55e6)

build() {
	cd msgpack-$pkgver
	cmake . \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_SHARED_LIBS=ON \
		-DMSGPACK_ENABLE_STATIC=OFF \
		-DMSGPACK_CXX11=ON \
		-DMSGPACK_BUILD_EXAMPLES=OFF
	make
}

package_msgpack-c() {
	cd msgpack-$pkgver
	make DESTDIR="$pkgdir" install
	rm -r "$pkgdir"/usr/{include,lib}
}

package_msgpack-c-devel() {
	depends=($pkgbase=$pkgver)

	cd msgpack-$pkgver
	make DESTDIR="$pkgdir" install
	rm -r "$pkgdir"/usr/bin
}
