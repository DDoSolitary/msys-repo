pkgname=tmate
pkgver=2.4.0
pkgrel=1
pkgdesc='Instant terminal sharing'
arch=(i686 x86_64)
url=https://tmate.io/
license=(BSD)
depends=(libevent libssh msgpack-c ncurses)
makedepends=(cmake libevent-devel libssh-devel msgpack-c-devel ncurses-devel)
source=(
	https://github.com/tmate-io/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz
	msys-compat.patch)
sha512sums=(
	92d2ca354b295678bfc2747ca83a45ebafeaec40ebac94bd94c4926af4f820b3f3a087f365147f41c80d1c8ad032f52c697a2421839c39315d84f98f7eccada6
	0e7602c5aaf2ebe741271ca674dab9dcf5feeaee7211ececf8ccffe2afc5f1e33e412039d0560349d649c93b0097d7a0f8e13c17c9f71200e0ac1ec61a086b59)

prepare() {
	cd $pkgbase-$pkgver
	patch -Np1 -i "$srcdir"/msys-compat.patch
	cp osdep-cygwin.c osdep-msys.c
}

build() {
	cd $pkgbase-$pkgver
	./autogen.sh
	export CFLAGS="$CFLAGS -D_GNU_SOURCE"
	./configure --prefix=/usr
	make
}

package() {
	cd $pkgbase-$pkgver
	make DESTDIR="$pkgdir" install
	install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
