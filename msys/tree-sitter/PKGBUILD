pkgbase=tree-sitter
pkgname=($pkgbase $pkgbase-devel)
pkgver=0.20.2
pkgrel=1
pkgdesc='An incremental parsing system for programming tools'
arch=(i686 x86_64)
url=https://github.com/tree-sitter/tree-sitter
license=(MIT)
source=(
	https://github.com/tree-sitter/$pkgbase/archive/v$pkgver.tar.gz
	1247.patch)
sha512sums=(
	4b9a7da47b48b0003daf9af60f9d48aacf7c74d1c9e2645eafd74f62f568bc69e2fb36bb8f0c6b9f1dc85829e146786cc6bf7050ed1402167790b1552990665f
	6f4e0a0bcc6ba2dd9bc0c3dd51bb3071f68e0e800f1a713c061beca8a9d31ce8d097ed4406c2e67b955c8fcffba6b257cf522d07526e2bd6be348d0634356da5)

prepare() {
	cd $pkgbase-$pkgver
	patch -Np1 -i "$srcdir"/1247.patch
}

build() {
	cd $pkgbase-$pkgver
	make PREFIX=/usr LDFLAGS=-no-undefined
	# Rust is not available
	#cd cli
	#cargo build --release --locked --all-features
}

package_tree-sitter() {
	groups=(libraries)

	cd $pkgbase-$pkgver
	make DESTDIR="$pkgdir" PREFIX=/usr install
	rm -r "$pkgdir"/usr/{include,lib}
	#install -Dm755 target/release/$pkgbase -t "$pkgdir"/usr/bin
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgbase
}

package_tree-sitter-devel() {
	groups=(development)
	depends=($pkgbase=$pkgver)
	pkgdesc='tree-sitter headers and libraries'

	cd $pkgbase-$pkgver
	make DESTDIR="$pkgdir" PREFIX=/usr install
	rm -r "$pkgdir"/usr/bin
}
