pkgbase=libtermkey
pkgname=($pkgbase $pkgbase-devel)
pkgver=0.22
pkgrel=1
pkgdesc='Library for easy processing of keyboard entry from terminal-based programs'
arch=(i686 x86_64)
url=http://www.leonerd.org.uk/code/libtermkey
license=(MIT)
makedepends=(unibilium-devel)
source=(http://www.leonerd.org.uk/code/$pkgbase/$pkgbase-$pkgver.tar.gz)
sha512sums=(2e8242cd1e6acb8900125dcee9fd47f7554c359363f760b88858c82667e1f3871bdd87358b14c8a133ef3cd2699947a5045477e64cdf7f96d4ead1723e73a219)

build() {
	cd $pkgbase-$pkgver
	make PREFIX=/usr LDFLAGS=-no-undefined
}

package_libtermkey() {
	groups=(libraries)
	depends=(unibilium)

	cd $pkgbase-$pkgver
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgbase
	rm -r "$pkgdir"/usr/{include,lib}
}

package_libtermkey-devel() {
	groups=(development)
	depends=($pkgbase=$pkgver)
	pkgdesc='libtermkey headers and libraries'

	cd $pkgbase-$pkgver
	make PREFIX=/usr DESTDIR="$pkgdir" install
	rm -r "$pkgdir"/usr/{bin,share}
}
