_pkgname=zsh-autosuggestions
pkgname=zsh-autosuggestions-git
pkgver=0.6.4.r0.gae315de
pkgrel=1
pkgdesc='Fish-like autosuggestions for zsh'
arch=(any)
url=https://github.com/zsh-users/zsh-autosuggestions
license=(MIT)
depends=(zsh)
makedepends=(git)
conflicts=($_pkgname)
provides=($_pkgname)
source=(git+$url.git)
md5sums=(SKIP)

pkgver() {
	cd $_pkgname
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd $_pkgname
	install -Dm644 $_pkgname{,.plugin}.zsh -t "$pkgdir"/usr/share/zsh/plugins/$_pkgname
	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$_pkgname
}
