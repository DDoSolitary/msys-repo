_pkgname=zsh-syntax-highlighting
pkgname=zsh-syntax-highlighting-git
pkgver=0.8.0.alpha1.pre.redrawhook.r50.gebef4e5
pkgrel=2
pkgdesc='Fish shell like syntax highlighting for Zsh'
arch=(any)
url=https://github.com/zsh-users/zsh-syntax-highlighting
license=(BSD)
depends=(zsh)
makedepends=(git)
conflicts=($_pkgname)
provides=($_pkgname)
source=(git+$url.git)
md5sums=(SKIP)

pkgver() {
	cd $_pkgname
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd $_pkgname
	make PREFIX=/usr SHARE_DIR="$pkgdir"/usr/share/zsh/plugins/$_pkgname DESTDIR="$pkgdir" install
	ln -s $_pkgname.zsh "$pkgdir"/usr/share/zsh/plugins/$_pkgname/$_pkgname.plugin.zsh
	install -dm755 "$pkgdir"/usr/share/licenses/$_pkgname
	ln -s /usr/share/doc/$_pkgname/COPYING.md "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
