_pkgname=oh-my-zsh
_gitname=ohmyzsh
pkgname=oh-my-zsh-git
pkgver=r6008.12669f29f
pkgrel=1
pkgdesc='A community-driven framework for managing your zsh configuration'
arch=(any)
url=https://github.com/ohmyzsh/ohmyzsh
license=(MIT)
depends=(zsh)
makedepends=(git)
optdepends=(
	'ruby: for some plugin functionality'
	'python: for some plugin functionality'
	'git: most themes use git (highly recommended but still optional)')
conflicts=($_pkgname)
provides=($_pkgname)
source=(
	git+$url.git
	0001-zshrc.patch)
sha512sums=(
	SKIP
	720d529c08ee5e662cee6839944735e4ffd811f9f0d70ec4c4056961a51f77f166b449eb13d8a80abb8c4c0e4564da2779ced14fd8b91d515211218ecdac20e6)

pkgver() {
	cd $_gitname
	printf r%s.%s $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

prepare() {
	cd $_gitname
	cp templates/zshrc.zsh-template zshrc
	patch -Np1 -i "$srcdir"/0001-zshrc.patch
}

package() {
	cd $_gitname
	find -type f ! -path './.*' -exec \
		install -Dm644 {} "$pkgdir"/usr/share/$_pkgname/{} \;
	install -dm755 "$pkgdir"/usr/share/licenses/$_pkgname
	ln -s /usr/share/$_pkgname/LICENSE.txt "$pkgdir"/usr/share/licenses/$_pkgname/LICENSE.txt
}
