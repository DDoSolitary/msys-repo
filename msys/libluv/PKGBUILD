pkgbase=libluv
pkgname=($pkgbase $pkgbase-devel)
pkgver=1.40.0
pkgrel=1
pkgdesc='Bare libuv bindings for lua'
arch=(i686 x86_64)
url=https://github.com/luvit/luv
license=(Apache)
makedepends=(cmake libuv-devel luajit-devel)
source=(
	https://github.com/luvit/luv/releases/download/${pkgver}-0/luv-${pkgver}-0.tar.gz
	cmake.patch)
sha512sums=(
	2d3ba322aa927ebc39948c00d20d8e2b2380990d764f8b6f05c76b2e8882ebc82c01c76436407decdb703e8b7045a4631743c5cec84a52034fc2a004cdbbcaea
	7e6cefb41a2fad87e29cac033c1ceb3c7849e4fd301b5daf0f768cb147e09647c2debdd705469376542ea8a969a9a394e4d8f6607301e63d17b0c5cfe044eb45)

prepare() {
	cd luv-$pkgver-0
	patch -Np1 -i "$srcdir"/cmake.patch
}

build() {
	cd luv-$pkgver-0
	cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DWITH_SHARED_LIBUV=ON \
		-DLUA_BUILD_TYPE=System \
		-DBUILD_MODULE=OFF \
		-DBUILD_SHARED_LIBS=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_libluv() {
	groups=(libraries)
	depends=(luajit libuv)

	cd luv-$pkgver-0
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgbase
	rm -r "$pkgdir"/usr/{include,lib}
}

package_libluv-devel() {
	groups=(development)
	depends=($pkgbase=$pkgver)
	pkgdesc='libluv headers and libraries'

	cd luv-$pkgver-0
	make DESTDIR="$pkgdir" install
	rm -r "$pkgdir"/usr/bin
}
