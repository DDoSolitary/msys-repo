pkgbase=libluv
pkgname=($pkgbase $pkgbase-devel)
pkgver=1.42.0_1
_pkgver=${pkgver//_/-}
pkgrel=1
pkgdesc='Bare libuv bindings for lua'
arch=(i686 x86_64)
url=https://github.com/luvit/luv
license=(Apache)
makedepends=(cmake libuv-devel luajit-devel)
source=(
	https://github.com/luvit/luv/releases/download/${_pkgver}/luv-${_pkgver}.tar.gz
	cmake.patch)
sha512sums=(
	2b08b0c7e9e3bf156761f49eb5c3da0e3f486d3ca018d38fa41a038bd41aa07a3888873e8451f38069dd63464f9b2be66e30af491b46ad7b34bc304722bd4692
	1fa210f9c87afed58090cdb09b0f2e17c12f767d3f2e54b56eb037be15fbbf17233de1c11853797bf89fe1b894d0b8c0153dfc011bab3fe1ebdeb5af8fc32c09)

prepare() {
	cd luv-$_pkgver
	patch -Np1 -i "$srcdir"/cmake.patch
}

build() {
	cd luv-$_pkgver
	cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DWITH_SHARED_LIBUV=ON \
		-DLUA_BUILD_TYPE=System \
		-DBUILD_MODULE=OFF \
		-DBUILD_SHARED_LIBS=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_libluv() {
	groups=(libraries)
	depends=(luajit libuv)

	cd luv-$_pkgver
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgbase
	rm -r "$pkgdir"/usr/{include,lib}
}

package_libluv-devel() {
	groups=(development)
	depends=($pkgbase=$pkgver)
	pkgdesc='libluv headers and libraries'

	cd luv-$_pkgver
	make DESTDIR="$pkgdir" install
	rm -r "$pkgdir"/usr/bin
}
