pkgbase=libluv
pkgname=($pkgbase $pkgbase-devel)
pkgver=1.43.0_0
_pkgver=${pkgver//_/-}
pkgrel=1
pkgdesc='Bare libuv bindings for lua'
arch=(i686 x86_64)
url=https://github.com/luvit/luv
license=(Apache)
makedepends=(cmake libuv-devel luajit-devel)
source=(
	https://github.com/luvit/luv/releases/download/${_pkgver}/luv-${_pkgver}.tar.gz
	cmake.patch)
sha512sums=(
	79e6c125dcd9c8ba06ade01c067ece74c92e3af09958ce7b921bc150d376ad9ae3fc3dbf5c65e92293174ab2905b0c2aad37a11571f9606594cbd8c4972274f2
	c4370ea9806de9d32dc527b10d95450d9b3da5673078059f4f88848fcfecea9f1940eb65a7582113ad2ee1d567d49d3a6e90f7a130739531f6962a4bdf09c699)

prepare() {
	cd luv-$_pkgver
	patch -Np1 -i "$srcdir"/cmake.patch
}

build() {
	cd luv-$_pkgver
	cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DWITH_SHARED_LIBUV=ON \
		-DLUA_BUILD_TYPE=System \
		-DBUILD_MODULE=OFF \
		-DBUILD_SHARED_LIBS=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_libluv() {
	groups=(libraries)
	depends=(luajit libuv)

	cd luv-$_pkgver
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgbase
	rm -r "$pkgdir"/usr/{include,lib}
}

package_libluv-devel() {
	groups=(development)
	depends=($pkgbase=$pkgver)
	pkgdesc='libluv headers and libraries'

	cd luv-$_pkgver
	make DESTDIR="$pkgdir" install
	rm -r "$pkgdir"/usr/bin
}
