pkgbase=libluv
pkgname=($pkgbase $pkgbase-devel)
pkgver=1.42.0
pkgrel=1
pkgdesc='Bare libuv bindings for lua'
arch=(i686 x86_64)
url=https://github.com/luvit/luv
license=(Apache)
makedepends=(cmake libuv-devel luajit-devel)
source=(
	https://github.com/luvit/luv/releases/download/${pkgver}-0/luv-${pkgver}-0.tar.gz
	cmake.patch)
sha512sums=(
	d9378832c47c0931523a618adb198c3c0d0320e49008c8b1a92e46ecf550ea00ac83543228faaa47886b5630756bd16e6dc33f67b8aea8943685f35085764fe5
	1fa210f9c87afed58090cdb09b0f2e17c12f767d3f2e54b56eb037be15fbbf17233de1c11853797bf89fe1b894d0b8c0153dfc011bab3fe1ebdeb5af8fc32c09)

prepare() {
	cd luv-$pkgver-0
	patch -Np1 -i "$srcdir"/cmake.patch
}

build() {
	cd luv-$pkgver-0
	cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DWITH_SHARED_LIBUV=ON \
		-DLUA_BUILD_TYPE=System \
		-DBUILD_MODULE=OFF \
		-DBUILD_SHARED_LIBS=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_libluv() {
	groups=(libraries)
	depends=(luajit libuv)

	cd luv-$pkgver-0
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgbase
	rm -r "$pkgdir"/usr/{include,lib}
}

package_libluv-devel() {
	groups=(development)
	depends=($pkgbase=$pkgver)
	pkgdesc='libluv headers and libraries'

	cd luv-$pkgver-0
	make DESTDIR="$pkgdir" install
	rm -r "$pkgdir"/usr/bin
}
