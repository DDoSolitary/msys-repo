_pkgname=zsh-theme-powerlevel10k
_gitname=powerlevel10k
pkgname=zsh-theme-powerlevel10k-git
pkgver=1.14.6.r19.g48ff2e8
pkgrel=2
pkgdesc='Powerlevel10k is a theme for Zsh. It emphasizes speed, flexibility and out-of-the-box experience.'
arch=(x86_64)
url=https://github.com/romkatv/powerlevel10k
license=(MIT)
depends=(zsh)
makedepends=(git)
conflicts=($_pkgname)
provides=($_pkgname)
source=(git+$url.git)
md5sums=(SKIP)

pkgver() {
	cd $_gitname
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd $_gitname
	make pkg
}

package() {
	cd $_gitname
	find -path ./.git -prune -o \( -type f -exec install -Dm644 {} "$pkgdir"/usr/share/$_pkgname/{} \; \)
}
