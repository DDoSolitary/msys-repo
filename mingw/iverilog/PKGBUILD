_realname=iverilog
pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname
pkgver=10.3
pkgrel=2
pkgdesc='Icarus Verilog compiler and simulation tool'
arch=(i686 x86_64)
url=http://iverilog.icarus.com/
license=(GPL)
depends=($MINGW_PACKAGE_PREFIX-{zlib,bzip2})
makedepends=(gperf git)
source=(git+https://github.com/steveicarus/iverilog#tag=v${pkgver/./_})
sha512sums=(SKIP)

prepare() {
	cd $_realname
	aclocal
	autoconf
}

build() {
	cd $_realname
	export CFLAGS="$CFLAGS -fcommon"
	./configure --prefix=$MINGW_PREFIX
	make
}

package() {
	cd $_realname
	make -j1 prefix="$pkgdir"$MINGW_PREFIX install
}
